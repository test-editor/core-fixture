plugins {
    id 'maven'
    id 'java'
    id 'com.jfrog.bintray' version '1.8.0'
    id 'net.researchgate.release' version '2.7.0'
}

apply from: 'code-style/codestyle.gradle'

dependencies {
	compile 'org.slf4j:slf4j-api:1.7.25'
	compile 'org.apache.logging.log4j:log4j-core:2.11.0'
	compile 'org.apache.logging.log4j:log4j-api:2.11.0'
	compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.0'
	compile 'junit:junit:4.12'
	compile 'org.apache.commons:commons-lang3:3.7'
	testCompile 'org.mockito:mockito-all:1.10.19'
}

/*
 * Default code for all fixtures below
 */
repositories {
    jcenter()
}

group = 'org.testeditor.fixture'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// show standard out during test to see logging output
test.testLogging.showStandardStreams = true

jar {
    manifest {
        attributes 'Implementation-Title': "${project.group}.${project.name}",
                   'Implementation-Version': project.version
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        pom.packaging = 'jar'
    }
}

release {
    preTagCommitMessage = '[release]'
    tagCommitMessage = '[release]'
    newVersionCommitMessage = '[release] new version'
    tagTemplate = 'v${version}'
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'Fixtures'
        name = project.name
        userOrg = 'test-editor'
        licenses = ['EPL-1.0']
        vcsUrl = "https://github.com/test-editor/${project.name}.git"
        version {
            name = project.version
            vcsTag = "v$project.version"
        }
        websiteUrl = 'http://testeditor.org'
    }
}
